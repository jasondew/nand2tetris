class SnakeGame {
  field Snake snake;
  field Object object;
  field int score;
  field String scoreString;

  static String scoreLabel;

  constructor SnakeGame new() {
    let snake = Snake.new();
    let object = Object.new();
    let score = 0;
    let scoreLabel = "Score: ";
    let scoreString = String.new(10);

    return this;
  }

  method void dispose() {
    do snake.dispose();
    do object.dispose();
    do scoreLabel.dispose();
    do scoreString.dispose();
    do Memory.deAlloc(this);

    return;
  }

  method void drawScore() {
    do scoreString.setInt(score);
    do Output.moveCursor(0, 53);
    do Output.printString(scoreLabel);
    do Output.moveCursor(0, 60);
    do Output.printString(scoreString);

    return;
  }

  method void run() {
    var char input;
    var boolean gameOver;

    let gameOver = false;

    while (~gameOver) {
      let input = Keyboard.keyPressed();

      if (input = 130) { do snake.goLeft(); }
      if (input = 132) { do snake.goRight(); }
      if (input = 131) { do snake.goUp(); }
      if (input = 133) { do snake.goDown(); }

      if (Position.equal(snake.getHeadPosition(), object.getPosition())) {
        do snake.grow();
        let score = score + 1;
        do object.dispose();
        let object = Object.new();
      } else {
        if (snake.crossing()) {
          let gameOver = true;
        } else {
          do snake.move();
        }
      }

      do Screen.clearScreen();
      do snake.draw();
      do object.draw();
      do drawScore();

      do Sys.wait(15);
    }

    do Output.moveCursor(12, 27);
    do Output.printString("GAME OVER");

    return;
  }
}
